define(<!L!>, <!'32'!>)
dnl Usage: Labeled_Extract(Salt, Label, IKM)
define(<!Labeled_Extract!>, <!Extract($1, <'HPKE_v1', $2, $3>)!>)
define(<!Labeled_Extract1!>, <!Extract($1, <'HPKE_v1', $2, $3>)!>)
dnl Usage: Labeled_Expand(PRK, Label, Info)
define(<!Labeled_Expand!>, <!Expand($1, <L, 'HPKE_v1', $2, $3>, L)!>)
dnl Usage: ExtractAndExpand(DH, KEM-context)
define(<!ExtractAndExpand!>,<!Labeled_Expand(Labeled_Extract('blank', 'eae_prk',$1), 'shared_secret', $2, L)!>)
dnl Usage: DeriveSecrets(context, Q_plain)
define(<!DeriveSecretsK!>,<!Expand(Extract($2, Labeled_Expand(KSExp($1), 'sec', 'odoh_secret')), 'odoh_key', L)!>)
define(<!DeriveSecretsN!>,<!Expand(Extract($2, Labeled_Expand(KSExp($1), 'sec', 'odoh_secret')), 'odoh_nonce', L)!>)
dnl Usage: KeySchedule(shared_secret, string)
define(<!KeySchedule!>,<!Labeled_Expand(Labeled_Extract($1, 'secret', 'blank'), $2, (<'0x01', info_hash>))!>)
dnl Usage: KSKey(shared_secret)
define(<!KSKey!>,<!KeySchedule($1, 'key')!>)
dnl Usage: KSNonce(shared_secret)
define(<!KSNonce!>,<!KeySchedule($1, 'base_nonce')!>)
dnl Usage: KSExp(shared_secret)
define(<!KSExp!>,<!KeySchedule($1, 'exp')!>)
